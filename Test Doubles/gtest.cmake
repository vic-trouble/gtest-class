include(ExternalProject)

set(GTEST_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install/gtest)
set(GTEST_INCLUDE_DIR ${GTEST_INSTALL_DIR}/include)
set(GTEST_LIB_DIR ${GTEST_INSTALL_DIR}/lib)

if(MSVC)
	set(EXTRA_FLAGS
		-DCMAKE_CXX_FLAGS=/D_SILENCE_TR1_NAMESPACE_DEPRECATION_WARNING
		-Dgtest_force_shared_crt=ON)
endif()

ExternalProject_Add(EP-gtest
	GIT_REPOSITORY https://github.com/google/googletest
	GIT_TAG release-1.8.0
	UPDATE_COMMAND ""
	CMAKE_ARGS -DCMAKE_INSTALL_PREFIX:PATH=${GTEST_INSTALL_DIR} ${EXTRA_FLAGS})

add_library(gtest INTERFACE)
set_property(TARGET gtest PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INCLUDE_DIR})
set_property(TARGET gtest PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_LIB_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}gtest${CMAKE_STATIC_LIBRARY_SUFFIX})
set_property(TARGET gtest APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_LIB_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}gmock${CMAKE_STATIC_LIBRARY_SUFFIX})

add_dependencies(gtest EP-gtest)
